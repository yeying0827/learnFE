## 11 | 图案生成：如何生成重复图案、分形图案以及随机效果？

可视化中的几何图形是用来表达数据的，那图案就是用来修饰这些几何图形，**强化视觉效果**的，所以图案一般是指几何图形上的花纹。

可视化中的图案非常多，三种最常用的：重复图案、分形图案和随机图案。

### 如何绘制大批量重复图案

在可视化应用中，我们经常会使用重复图案。比如说，我们在显示图表的时候，经常会给背景加上一层网格，这样可以辅助用户阅读和理解图表数据。

像网格这样经典的重复图案，应该怎样绘制它呢？

**如果我们将网格绘制在Canvas2D画布上**，网格的线条很多，这意味着我们要用大量的绘图指令来绘制。一旦Canvas2D的画面改变了，就需要重绘全部的网格，这会大大消耗系统的性能。那该怎么办呢？你可能会想到准备两个Canvas2D画布，不过第一次绘图的开销仍然存在。

#### 1. 使用background-image来绘制重复图案

比如：

```css
canvas {
  background-image: linear-gradient(to right, transparent 90%, #ccc 0),
    linear-gradient(to bottom, transparent 90%, #ccc 0);
  background-size: 8px 8px, 8px 8px;
}
```

CSS的linear-gradient函数可以定义线性渐变。

在linear-gradient中定义颜色过渡的时候，如果后一个过渡颜色的区域值和前面相同，可以简写为0。

因为浏览器将渐变函数视为图片，所以可以将渐变设置在任何可以接受图片的CSS属性上。

因为CSS的background-repeat默认值是repeat，利用浏览器自己的background-repeat机制，就可以实现网格背景了。

这种利用了CSS属性设置重复网格背景的技巧，在一般情况下能够满足我们的需求，但也存在一些限制：

首先，因为设置的是背景，网格和直接绘制在画布上的其他图形处于不同的层，无法将网格覆盖在这些图形上；

其次，当我们用坐标变换来缩放或移动元素时，作为背景的网格是不会随着缩放或移动而改变的。

#### 2. 使用Shader来绘制重复图案

如果用WebGL来渲染的话，还有更简单的做法，就是利用GPU并行计算的特点，使用着色器来绘制背景网格这样的重复图案。

`src/pages/Pattern/WebGL.vue`

```glsl
// 顶点着色器
attribute vec2 a_vertexPosition;
attribute vec2 uv;
varying vec2 vUv;

void main() {
  gl_PointSize = 1.0;
  vUv = uv;
  gl_Position = vec4(a_vertexPosition, 1, 1);
}

// 片元着色器
#ifdef GL_ES
precision mediump float;
#endif

varying vec2 vUv; // 由顶点着色器传来的uv属性
uniform float rows;

// 最终得到当前像素点所要渲染的色值
void main() {
  // vUv * rows：纹理坐标相当于一个单元的坐标，坐标的范围是一个正方形
  // 所以*rows相当于把纹理坐标在画布上的横向和纵向分别放大rows倍，然后可以得到当前所要处理的像素在其中对应的坐标（映射）
  // 后续利用这个对应的坐标去计算色值

  // st：保存像素点对应纹理坐标的小数部分
  vec2 st = fract(vUv * rows); // fract函数是一个用于获取向量中小数部分的函数
  float d1 = step(st.x, 0.9); // step：阶梯函数。当step(a,b)中的b < a时，返回0；当b >= a时，返回1。
  // step函数相当于判断b是否大于等于a，若是，就返回1（真值）；否则返回0（假值）。
  float d2 = step(0.1, st.y);
  // mix(a,  b,  c)：线性插值函数。a和b是两个输入的颜色或值，c是一个介于0和1之间的浮点数，表示插值的权重
  // 当c接近0时，返回a；当c接近1时，mix函数返回b；当c在0和1之间时，返回a和b的插值结果。

  // 根据d1*d2的值，决定使用哪个颜色来绘制当前像素。
  // st.x <= 0.9 且 st.y >= 0.1时，d1*d2=1, 否则为0
  // 1. st.x <= 0.9 且 st.y >= 0.1  -->  d1*d2=1 --> rgb = vec3(1.0)白
  // 2. 其他情况  --> d1*d2=0 --> rgb = vec(0.8)灰
  gl_FragColor.rgb = mix(vec3(0.8), vec3(1.0), d1 * d2);
  gl_FragColor.a = 1.0;

  // rows决定网格的数量，所以最终的效果和rows的取值有关。
}
```

本节使用课程提供的一个基础库[gl-renderer](https://github.com/akira-cn/gl-renderer)；在WebGL底层的基础上进行了一些简单的封装。使开发者将重心放在提供数据、设置变量和编写Shader上。

gl-renderer的使用方法十分简单，一共分为五步。

第一步和第二步分别是创建Renderer对象和创建并启用WebGL程序。代码如下所示：

```javascript
// 第一步：创建Renderer对象
const canvas = document.querySelector('canvas');
const renderer = new GlRenderer(canvas);
// 第二步：创建并启用WebGL程序
const program = renderer.compileSync(fragment, vertex);
renderer.useProgram(program);
```

第三步是设置uniform变量。

```javascript
// 第三步：设置uniform变量
renderer.uniforms.rows = 32; // 64; // 每一行显示多少个网格（在片元着色器中使用）
```

第四步是将顶点数据送入缓冲区。

```javascript
// 第四步：将顶点数据送入缓冲区
renderer.setMeshData([{
  positions: [ // 顶点（覆盖整个画布）
      [-1, -1], // 左下
      [-1, 1], // 左上
      [1, 1], // 右上
      [1, -1] // 右下
  ],
  attributes: {
    uv: [ // 纹理坐标（坐标系：左下角[0,0] 右上角[1,1]）
        [0, 0], // 左下
        [0, 1], // 左上
        [1, 1], // 右上
        [1, 0] // 右下
    ]
  },
  cells: [ // 顶点索引（三角剖分）：将矩形剖分成两个三角形。WebGL只能渲染经过三角剖分之后的多边形。
      [0, 1, 2],
      [2, 0, 3]
  ]
}]);
```

第五步执行渲染。

```javascript
// 第五步：执行渲染
renderer.render();
```

具体分析，参看上述GLSL代码的注释。

使用Shader实现重复图案，优势在于：不管我们给rows取值多少，图案都是一次绘制出来的，并不会因为rows增加而消耗性能。（绘制消耗的时间几乎是常量，不会遇到性能瓶颈。）



### 如何绘制分形图案

通俗来说，一个分形图案可以划分成无数个部分，而每个部分的形状又都和这个图案整体具有相似性。

典型的分形效果具有局部与整体的自相似性以及无限细节（分形可以无限放大），能产生令人震撼的视觉效果。

分形在实践中偏向于视觉和UI设计。在实际的可视化项目中不太常用，但能够起到画龙点睛的作用。

**分形在视觉呈现中的实现技巧**

分形公式，Mandelbrot Set，也叫曼德勃罗特集（由美国数学家曼德勃罗特教授发现的迭代公式构成的分形集合）。
$$
Z_{n+1} = Z_n^2 + C
$$
以上公式中Z(n)和Z(n+1)是复数，C是一个实数常量。

这个迭代公式使用起来非常简单，只要我们**给定一个初始值**，就能产生许多有趣的图案。

以下是一个例子：

`src/pages/Pattern/fractalPattern.vue`

* 首先是片元着色器

```glsl
// 片元着色器
#ifdef GL_ES
precision mediump float;
#endif
varying vec2 vUv;
uniform vec2 center;
uniform float scale;

vec2 f(vec2 z, vec2 c) {
  return mat2(z, -z.y, z.x) * z + c;
}

void main() {
  vec2 uv = vUv;
  // 设置初始的z和c，然后执行迭代。
  vec2 c = center + 4.0 * (uv - vec2(0.5)) / scale;
  vec2 z = vec2(0.0);

  bool escaped = false;
  int j;
  // 给一个足够精度的最大迭代次数：65536
  for (int i = 0; i < 65536; i ++) {
    if (i > iterations) break;
    j = i;
    z = f(z, c);
    if (length(z) > 2.0) { // 如果z的模大于2，就结束计算；否则就继续迭代，直到达到循环次数
      escaped = true;
      break;
    }
  }

  gl_FragColor.rgb = escaped ? vec3(float(j)) / float(iterations) : vec3(0.0);
  gl_FragColor.a = 1.0;
}
```

**`return mat2(z, -z.y, z.x) * z + c;`**说明：
$$
 \begin{bmatrix}
 x&-y\\
 y&x\\
 \end{bmatrix} * 
 \begin{bmatrix}
 x&y\\
 \end{bmatrix} =
 \begin{bmatrix}
    x^2+y^2 \\
    0 
  \end{bmatrix}
$$
注：在GLSL中以列为主序，叉乘以**列乘以行**



以上代码设置了初始的z和c，然后执行迭代。

理论上曼德勃罗特集应该是无限迭代的，但是我们不能让它无限循环，所以要给一个足够精度的最大迭代次数，比如此处的65536。

如果z的模大于2，就结束计算，否则就继续迭代，直到达到循环次数。

把`(0, 0)`设置为图案中心点，放大系数初始设为1，即原始大小，然后开始渲染。js代码如下：

```javascript
// 第一步：创建Renderer对象
// ...
// 第二步：创建并启用WebGL程序
// ...
// 第三步：设置uniform变量
renderer.uniforms.center = [0, 0]; // 设置(0,0)为图案中心点
renderer.uniforms.scale = 1; // 放大系数初始值为1，即原始大小
renderer.uniforms.iterations = 256;
// 第四步：将顶点数据送入缓冲区
// ...
// 第五步：执行渲染
// ...
```

进阶：

修改一下Shader中的代码，改变渲染颜色的规则，根据迭代次数和迭代深度的比值来渲染不同的颜色，然后将它局部放大，就能得到非常有趣的图案。



### 如何给图案增加随机效果

分形图案的无限细节里同时拥有重复和随机这两个规律。对于其他非分形的图案，如果想让它变得吸引人，可以给它们增加随机效果。

`src/pages/Pattern/RandomPattern.vue`

#### 例子1：一片噪点

在Shader中使用伪随机函数。以下是一个常用的伪随机函数：

```glsl
float random(vec2 st) {
  return fract(sin(dot(st.xy, vec2(12.9898, 78.233)))
                * 43758.5453123);
}
```

原理：取正弦函数偏后部的小数部分的值来模拟随机。

传入一个确定的st值，就会返回一个符合随机分布的确定的float值。

使用：

```glsl
#ifdef GL_ES
precision mediump float;
#endif

varying vec2 vUv;

// 伪随机函数：取正弦函数偏后部的小数部分的值来模拟随机
float random(vec2 st) {
  return fract(sin(dot(st.xy, vec2(12.9898, 78.233)))
                * 43758.5453123);
}

// 最终得到当前像素点所要渲染的色值
void main() {
  // 效果：一片噪点
  gl_FragColor.rgb = vec3(random(vUv));
  gl_FragColor.a = 1.0;
}
```

#### 例子2：生成随机色块

使用floor取整函数

```glsl
#ifdef GL_ES
precision mediump float;
#endif

varying vec2 vUv;

// 伪随机函数：取正弦函数偏后部的小数部分的值来模拟随机
float random(vec2 st) {
  return fract(sin(dot(st.xy, vec2(12.9898, 78.233)))
                * 43758.5453123);
}

// 最终得到当前像素点所要渲染的色值
void main() {
  // 效果：生成随机的色块
  // vUv * 10.0：纹理坐标相当于一个单元的坐标，坐标的范围是一个正方形
  // 所以*10.0相当于把纹理坐标在画布上的横向和纵向分别放大10倍，然后可以得到当前所要处理的像素在其中对应的坐标
  // 后续利用这个对应的坐标去计算色值
  vec2 st = vUv * 10.0;
  gl_FragColor.rgb = vec3(random(floor(st)));

  gl_FragColor.a = 1.0;
}
```

floor：向下取浮点数的整数部分。

一共10行*10列=100个方块。通过random函数给每一个方块随机一个颜色。

#### 例子3：动态移动的长方块

原理：结合随机和动态效果

具体方法：传入一个代表时间的uTime变量

```glsl
#ifdef GL_ES
precision mediump float;
#endif

varying vec2 vUv;

uniform float uTime;

// 伪随机函数：取正弦函数偏后部的小数部分的值来模拟随机
float random(vec2 st) {
  return fract(sin(dot(st.xy, vec2(12.9898, 78.233)))
                * 43758.5453123);
}

void main() {
  // 效果：动态移动的长方块
  vec2 st = vUv * vec2(100.0, 50.0); // 100列（X轴分100份），50行（Y轴分50份）
  // st.x原始范围：0,100
  // st.y原始范围：0,50

  // floor(st.y)：0-1，1-2，,2-3，,3-4，4-5...49-50每一纵段
  // 重新计算st.x的值
  st.x -= (1.0 + 10.0 * random(vec2(floor(st.y)))) * uTime;

  vec2 ipos = floor(st); // integer
  vec2 fpos = fract(st); // fraction

  vec3 color = vec3(step(random(ipos), 0.7)); // 1,1,1 或 0,0,0
  color *= step(0.2, fpos.y); // y小数部分小于0.2时，step结果为0，color乘以0，则为黑色，黑色作为分割线

  gl_FragColor.rgb = color;
  gl_FragColor.a = 1.0;
}
```

js代码：

```javascript
// 设置uniform变量
renderer.uniforms.uTime = 0.0;
// 动态设置
requestAnimationFrame(function update(t) {
  renderer.uniforms.uTime = 4 * t / 1000;
  requestAnimationFrame(update);
});
```

#### 例子4：迷宫

用smoothstep函数，生成可以随机旋转方向的线段，从而生成一个迷宫。

```glsl
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265358979323846

varying vec2 vUv;
uniform int rows;

// 伪随机数
float random (in vec2 _st) {
  return fract(sin(dot(_st.xy, vec2(12.9898, 78.233)))
                * 43758.5453123);
}

vec2 truchetPattern(in vec2 _st, in float _index) {
  _index = fract((_index - 0.5) * 2.0);
  if (_index > 0.75) {
    _st = vec2(1.0) - _st; // 类似于取补码
  } else if (_index > 0.5) {
    _st = vec2(1.0 - _st.x, _st.y); // x取补码，y取原码
  } else if (_index > 0.25) {
    // https://www.jianshu.com/p/3d7605a02516
    // float/vec2/vec3/vec4  包含 1，2，3，4 个浮点型向量
    // 所以1.0可以表示包含一个浮点数的向量？？
    _st = 1.0 - vec2(1.0 - _st.x, _st.y);
  }
  return _st;
}

void main() {
  // 效果：迷宫
  vec2 st = vUv * float(rows);
  vec2 ipos = floor(st); // integer
  vec2 fpos = fract(st); // fraction

  vec2 tile = truchetPattern(fpos, random(ipos));
  float color = 0.0;

  color = smoothstep(tile.x - 0.3, tile.x, tile.y) -
          smoothstep(tile.x, tile.x + 0.3, tile.y);

  gl_FragColor = vec4(vec3(color), 1.0);
}
```

>  说明：
>
> `float smoothstep(float edge0, float edge1, float x);`
>
> 参数说明：
>
> - `edge0`：定义平滑区域的开始边缘。
> - `edge1`：定义平滑区域的结束边缘。
> - `x`：要测试的值。
>
> 当`x`在`edge0`和`edge1`之间时，函数返回一个在0和1之间的平滑插值。如果`x`小于`edge0`，返回值是0；如果`x`大于`edge1`，返回值是1。这个函数通常用于在两个值之间创建一个平滑的过渡，例如在渲染中的纹理混合、颜色渐变或者边缘检测中。



### 要点总结

三种常用图案的生成原理：

**第一种，批量重复图案。**

* 使用CSS的background-image属性，利用background-repeat快速重复绘制
* 使用片元着色器，利用GPU的并行渲染的特点来绘制

**第二种，分形图案。**

用一个直接的公式，曼德勃罗特集。

**第三种，在重复图案上增加随机性。**

在片元着色器中使用伪随机函数，来给重复图案实现随机效果。



虽然说几何图形是用来承载数据信息，图案是来强化视觉效果的，但实际上，它们并没有绝对的界限，有时候我们也可以将图案与数据信息一起管理。=> 大大强化可视化的视觉效果，从而加深用户对信息的理解。

片元着色器是最适合生成和绘制这些图案的技术。用SVG或Canvas同样可以绘制这些图案，但没法做到像WebGL这样并行高效地渲染这些图案。



### 小试牛刀

`src/pages/Pattern/PracticePattern.vue`

1. 例子2进阶，渲染出彩色方块。（提示：将随机数映射成HSV坐标中的H，然后绘制出不同的彩色方阵。）

   ```glsl
   #ifdef GL_ES
   precision mediump float;
   #endif
   
   varying vec2 vUv;
   
   uniform int rows;
   
   // hsv -> rgb
   // 参数的取值范围都是 (0, 1)
   vec3 hsv2rgb(vec3 c) {
     vec3 rgb = clamp(abs(mod(c.x * 6.0 + vec3(0.0, 4.0, 2.0), 6.0) - 3.0) - 1.0, 0.0, 1.0);
     rgb = rgb * rgb * (3.0 - 2.0 * rgb);
     return c.z * mix(vec3(1.0), rgb, c.y);
   }
   
   // 伪随机数
   float random (in vec2 _st) {
     return fract(sin(dot(_st.xy, vec2(12.9898, 78.233)))
                   * 43758.5453123);
   }
   
   void main() {
     gl_FragColor.rgb = hsv2rgb(vec3(random(floor(vUv * float(rows))), 0.5, 1.0));
     gl_FragColor.a = 1.0;
   }
   ```

   ```javascript
   renderer.uniforms.rows = 10;
   ```

2. 把伪随机迷宫图案修改成真正随机迷宫图案。（参考评论，传递一个随机系数）

   ```glsl
   #ifdef GL_ES
   precision mediump float;
   #endif
   
   #define PI 3.14159265358979323846
   
   varying vec2 vUv;
   uniform int rows;
   uniform float k; // 接收一个随机系数
   
   // 伪随机数
   float random (in vec2 _st) {
     return fract(sin(dot(_st.xy, vec2(12.9898, 78.233)))
                   * 43758.5453123);
   }
   
   vec2 truchetPattern(in vec2 _st, in float _index) {
     _index = fract((_index - 0.5) * 2.0);
     if (_index > 0.75) {
       _st = vec2(1.0) - _st; // 类似于取补码
     } else if (_index > 0.5) {
       _st = vec2(1.0 - _st.x, _st.y); // x取补码，y取原码
     } else if (_index > 0.25) {
       _st = 1.0 - vec2(1.0 - _st.x, _st.y);
     }
     return _st;
   }
   
   void main() {
     // 效果：迷宫
     vec2 st = vUv * float(rows);
     vec2 ipos = floor(st); // integer
     vec2 fpos = fract(st); // fraction
   
     vec2 tile = truchetPattern(fpos, random(ipos) * k);
     float color = 0.0;
   
     color = smoothstep(tile.x - 0.3, tile.x, tile.y) -
             smoothstep(tile.x, tile.x + 0.3, tile.y);
   
     gl_FragColor = vec4(vec3(color), 1.0);
   }
   ```

   ```javascript
   renderer.uniforms.rows = 10;
   renderer.uniforms.k = Math.random() + 1; // 随机系数
   ```

3. 扩展网格例子，实现图案随着图形的缩放和移动变化的效果。（提示：可以使用顶点着色器和仿射变换矩阵来实现）

   ```glsl
   attribute vec2 a_vertexPosition;
   attribute vec2 uv;
   
   varying vec2 vUv;
   
   uniform int scale;
   uniform vec2 offset;
   
   mat3 translateMatrix = mat3( // 平移矩阵
     1.0, 0.0, 0.0, // 第一列
     0.0, 1.0, 0.0, // 第二列
     offset.x, offset.y, 1.0 // 第三列
   );
   
   mat3 scaleMatrix = mat3( // 缩放矩阵
     float(scale), 0.0, 0.0,
     0.0, float(scale), 0.0,
     0.0, 0.0, 1.0
   );
   
   void main() {
     gl_PointSize = 1.0;
     vUv = uv;
     vec3 pos = scaleMatrix * translateMatrix * vec3(a_vertexPosition, 1.0);
     gl_Position = vec4(pos, 1.0);
   }
   ```

   ```javascript
   const lastPos = {}, lastCenter = {};
   // ...
   renderer.uniforms.scale = 1;
   renderer.uniforms.offset = [0.0, 0.0];
   renderer.uniforms.rows = 64;
   lastCenter.x = 0.0;
   lastCenter.y = 0.0;
   addEvent();
   // ...
   const wheelEventHandler = e => {
     e.preventDefault();
     if (e.wheelDeltaY > 0) { // 向下滚
       // console.log('向下滚'); // 放大
       if (renderer.uniforms.scale <= 50) {
         renderer.uniforms.scale += 1;
       }
     } else {
       // console.log('向上滚'); // 缩小
       if (renderer.uniforms.scale > 1) { // 使用浮点数会存在精度问题。scale=1.2以下绘制的网格边有问题，放大缩小过程中有时网格边粗细不一样
         renderer.uniforms.scale -= 1;
       }
     }
   }
   const mouseDownHandler = e => {
     e.preventDefault();
     // 记录初始位置
     lastPos.x = e.offsetX;
     lastPos.y = e.offsetY;
     // 绑定事件
     patternPracticeRef.value.addEventListener('mousemove', mouseMoveHandler);
   }
   const mouseMoveHandler = e => {
     e.preventDefault();
     // 计算坐标差值并转换为Canvas差值
     const { offsetX: x, offsetY: y } = e;
     const translateX = (x - lastPos.x) / patternPracticeRef.value.width;
     const translateY = (lastPos.y - y) / patternPracticeRef.value.height;
     // 设置偏移量
     renderer.uniforms.offset = [translateX + lastCenter.x, translateY + lastCenter.y];
   }
   const mouseUpHandler = e => {
     e.preventDefault();
     // 计算坐标差值并转换为Canvas差值
     const { offsetX: x, offsetY: y } = e;
     const translateX = (x - lastPos.x) / patternPracticeRef.value.width;
     const translateY = (lastPos.y - y) / patternPracticeRef.value.height;
     // 更新新的中心点
     lastCenter.x = translateX + lastCenter.x;
     lastCenter.y = translateY + lastCenter.y;
     // 解除事件绑定
     patternPracticeRef.value.removeEventListener('mousemove', mouseMoveHandler);
   }
   const addEvent = () => {
     patternPracticeRef.value.addEventListener('mousewheel', wheelEventHandler);
     patternPracticeRef.value.addEventListener('mousedown', mouseDownHandler);
     patternPracticeRef.value.addEventListener('mouseup', mouseUpHandler);
   }
   // ...
   ```

   

### 推荐阅读

片元着色器教程：[The Book of Shaders](https://thebookofshaders.com/?lan=ch)