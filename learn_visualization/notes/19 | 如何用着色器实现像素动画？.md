## 19 | 如何用着色器实现像素动画？

回顾上节：

* 固定帧动画：

  * 为每一帧准备一张图片
  * 把CSS关键帧动画的easing-function设为step-end进行循环播放

* 增量动画：

  在每帧给元素的相关属性增加一定的量

* 时序动画：

  通过控制时间和动画函数来描述动画。

  * 定义初始时间和周期
  * 在update中计算当前经过时间和进度p
  * 通过p来更新动画元素的属性

* 标准动画模型：

  * 定义Animator类
  * 使用**线性插值**实现匀速运动的动画
  * 通过**缓动函数**实现变速运动的动画

本节：怎么用着色器来实现动画。



### 如何用着色器实现固定帧动画

可以使用**在片元着色器中替换纹理坐标**的方式。（非常简单）

**e.g.会飞的小鸟**

```glsl
// 片元着色器
varying vec2 vUv;
uniform sampler2D tMap;
uniform float fWidth;
uniform vec2 vFrames[3]; // 3个二维向量，二维向量表示每一帧动画的图片起始x和结束x坐标
uniform int frameIndex;

void main() {
  vec2 uv = vUv;
  for (int i = 0; i < 3; i ++) {
    // 纹理坐标ux.x的取值范围
    // 第0帧：[2/272, 88/272] 约等于 [0.007,0.323]
    // 第1帧：[90/272, 176/272] 约等于 [0.330,0.647]
    // 第2帧：[178/272, 264/272] 约等于 [0.654,0.970]
    uv.x = mix(vFrames[i].x, vFrames[i].y, vUv.x) / fWidth; // vUv 到 uv的映射
    if(float(i) == mod(float(frameIndex), 3.0)) break; // frameIndex除3的余数：0-循环一次；1-循环两次；2-循环三次。（渲染第几帧）
  }

  vec4 color = texture2D(tMap, uv); // 按照uv坐标取色值

  gl_FragColor = color;
}
```

关键部分：main函数中的 for 循环。（动画只有3帧，所以最多只需要循环3次）。

一个重要的参数：vFrames。每一帧动画的图片起始 x 和结束 x 坐标，用这两个坐标和vUv.x计算插值，最后除以图片的总宽度fWidth，就能得到对应的纹理 x 坐标。

实现这个固定帧动画对应的JavaScript代码如下：

```javascript
// ...
(async function() {
  renderer.uniforms.tMap = await renderer.loadTexture(birdpng);
  renderer.uniforms.vFrames = [2, 88, 90, 176, 178, 264];
  renderer.uniforms.fWidth = 272;
  renderer.uniforms.frameIndex = 0;
  setInterval(() => {
    renderer.uniforms.frameIndex ++;
  }, 200);
  // 顶点坐标（WebGL画布绘制范围）
  const x = 43 / glRef.value.width; // 每帧的宽度（86/2）
  const y = 30 / glRef.value.height; // 每帧的高度(60/2)
  renderer.setMeshData([{
    positions: [
      [-x, -y],
      [-x, y],
      [x, y],
      [x, -y]
    ],
    attributes: {
      uv: [
        [0, 0],
        [0, 1],
        [1, 1],
        [1, 0]
      ]
    },
    cells: [
      [0, 1, 2],
      [2, 0, 3]
    ]
  }]);
  renderer.render();
}());
```

CSS：使用background-image，通过切换background-position实现动画

Shader：将图片纹理 tMap传进去，然后根据不同的frameIndex来计算出对应的纹理坐标（这个计算是在片元着色器中进行的）。



### 如何用着色器实现非固定帧动画

增量动画和时序动画，都要将与时间有关的参数传给着色器，处理过程非常相似。

用Shader实现非固定帧动画，本质上和上一节的实现方式没有太大区别。可以使用同样的方法，以及标准动画模型来实现它。（优点：更加灵活，可以操作更多的属性，实现更丰富的效果。）

#### 1. 用顶点着色器实现非固定帧动画

顶点着色器是怎么实现动画的。

**e.g.旋转的红色方块**

```glsl
attribute vec2 a_vertexPosition;
attribute vec2 uv;
uniform float rotation;

void main() {
  gl_PointSize = 1.0;
  float c = cos(rotation);
  float s = sin(rotation);
  mat3 transformMatrix = mat3(
    c,  s, 0,
    -s, c, 0,
    0,  0, 1
  );
  vec3 pos = transformMatrix * vec3(a_vertexPosition, 1); // 映射新的坐标
  gl_Position = vec4(pos, 1);
}
```

在顶点着色器中，先绘制出一个红色的正方形，然后用三维齐次矩阵实现旋转。

具体：把顶点坐标进行矩阵运算，并配合下面的JavaScript代码，就能让正方形旋转了。

```javascript
// ...
renderer.uniforms.rotation = 0.0;
requestAnimationFrame(function update() {
  renderer.uniforms.rotation += 0.05;
  requestAnimationFrame(update);
});
// ...
```

**e.g.使用标准动画模型来实现。**

具体：定义一个新的Animator对象，然后在Animator对象的方法中更新rotation属性。

使用标准模型能更加精确地控制图形的旋转效果。

```javascript
// ...
renderer.uniforms.rotation = 0.0;
const animator = new Animator({duration: 2000, iterations: Infinity});
animator.animate(renderer, ({target, timing}) => {
  target.uniforms.rotation = timing.p * 2 * Math.PI;
});
// ...
```

CSS：直接修改HTML元素的属性

Shader：将属性通过uniform变量传给着色器执行渲染。

#### 2. 用片元着色器实现非固定帧动画

将时间参数 uTime 通过uniform传给着色器的时候，就是在实现动画。

**e.g.旋转的红色方块**

将旋转放到片元着色器中处理，也能实现类似的旋转效果。

```glsl
varying vec2 vUv;
uniform vec4 color;
uniform float rotation;

void main() {
  vec2 st = 2.0 * (vUv - vec2(0.5));
  float c = cos(rotation);
  float s = sin(rotation);
  mat3 transformMatrix = mat3(
    c, s, 0,
    -s, c, 0,
    0, 0, 1
  );
  vec3 pos = transformMatrix * vec3(st, 1.0); // 坐标系旋转
  float d1 = 1.0 - smoothstep(0.5, 0.505, abs(pos.x)); // abs(x)<0.5 d1=1
  float d2 = 1.0 - smoothstep(0.5, 0.505, abs(pos.y)); // abs(y)<0.5 d2=1
  gl_FragColor = d1 * d2 * color;
}
```

**顶点着色器和片元着色器实现的旋转动画方向正好相反。为什么会出现这样的情况呢？**

因为在顶点着色器中，直接改变了顶点坐标，角度增大呈逆时针方向旋转；在片元着色器中，绘制原理是通过**距离场着色**来实现的，所以这里的旋转实际上改变的是距离场的角度而不是图形角度，最终绘制的图形也是相对于距离场的。又因为距离场逆时针旋转，所以图形就顺时针旋转了。（旋转的是坐标系？）

**注：**一般来说，动画如果能使用顶点着色器实现，会尽量在顶点着色器中实现。因为在绘制一帧画面的时候，顶点着色器的运算量会大大少于片元着色器，所以使用顶点着色器消耗的性能更少。

**在片元着色器中实现非固定帧动画也有优势**：可以使用片元着色器的技巧，如重复、随机、噪声等等来绘制更加复杂的效果。

**e.g.利用网格实现大量的重复动画。**

把上面的代码稍微修改一下，使用取小数和取整数的函数，再用之前网格化的思路。

```glsl
varying vec2 vUv;
uniform float rotation;

${noise.random2d}
${color.hsb}

void main() {
  vec2 f_uv = fract(vUv * 10.0);
  vec2 i_uv = floor(vUv * 10.0);
  vec2 st = 2.0 * (f_uv - vec2(0.5));
  float c = 0.7 * cos(rotation);
  float s = 0.7 * sin(rotation);
  mat3 transformMatrix = mat3(
    c, s, 0,
    -s, c, 0,
    0, 0, 1
  );
  vec3 pos = transformMatrix * vec3(st, 1.0);
  float d1 = 1.0 - smoothstep(0.5, 0.505, abs(pos.x));
  float d2 = 1.0 - smoothstep(0.5, 0.505, abs(pos.y));
  gl_FragColor = d1 * d2 * vec4(hsb2rgb(vec3(random(i_uv), 1.0, 1.0)), 1.0);
}
```

充分利用GPU的并行效率，比用其他方式把图形一个一个地绘制出来性能要高得多。



### 如何在着色器中实现缓动函数与非线性插值

#### 轨迹动画

上面的例子中，使用Shader的矩阵运算实现了旋转动画。

轨迹动画也可以用Shader矩阵运算实现。

**e.g.在画布上绘制一个红色的方块，利用它实现轨迹动画**

* 首先实现一个着色器，它通过设置translation来改变图形位置

  ```glsl
  // vertex
  attribute vec2 a_vertexPosition;
  attribute vec2 uv;
  uniform vec2 translation;
  
  void main() {
    gl_PointSize = 1.0;
    mat3 transformMatrix = mat3(
      1, 0, 0,
      0, 1, 0,
      translation, 1
    );
    vec3 pos = transformMatrix * vec3(a_vertexPosition, 1);
    gl_Position = vec4(pos, 1);
  }
  ```

* 在JavaScript中，将translation依照时间变化并传给上面的着色器，就可以让方块移动。

  ```javascript
  // ...
  renderer.uniforms.color = [1, 0, 0, 1];
  renderer.uniforms.translation = [-0.5, 0];
  
  const animator = new Animator({duration: 2000});
  animator.animate(renderer, ({target, timing}) => {
    target.uniforms.translation = [-0.5 * (1 - timing.p) + 0.5 * timing.p, 0];
  });
  
  bufferData({
    positions: [
      [-0.25, -0.25],
      [-0.25, 0.25],
      [0.25, 0.25],
      [0.25, -0.25]
    ]
  });
  // ...
  ```

效果：让方块沿水平方向向右匀速运动一段距离。

#### 缓动函数（Shader）

此外，还可以通过缓动函数来实现非匀速运动。（既可以将缓动函数用JavaScript计算，也可以直接将缓动函数放在Shader中。）

将缓动函数用JavaScript计算，就是给Animator传一个easing函数进去就可以。

将缓动函数写在Shader中，

**e.g.顶点着色器实现非固定帧动画**

直接将时间参数uTime传入Shader，然后在Shader中加入缓动函数。

用`smoothstep(0.0, 1.0, p)`来让方块做平滑加速、减速运动；也可以替换缓动函数，使用比如`clamp(p*p, 0.0, 1.0)`或`clamp(p*(2.0-p), 0.0, 1.0)`来实现匀加速、匀减速的运动效果。

```glsl
// vertex
attribute vec2 a_vertexPosition;
uniform vec4 uFromTo;
uniform float uTime;

float easing(in float p) {
  // return smoothstep(0.0, 1.0, p);
  // return clamp(p * p, 0.0, 1.0); // 匀加速

  return clamp(p * (2.0 - p), 0.0, 1.0); // 0->1->0 // 先减速后加速

  // if(p < 1.0) return clamp(p * (2.0 - p), 0.0, 1.0);
  // else return 1.0;
}

void main() {
  gl_PointSize = 1.0;
  vec2 from = uFromTo.xy;
  vec2 to = uFromTo.zw;
  float p = easing(uTime / 2.0);
  vec2 translation = mix(from, to, p);
  mat3 transformMatrix = mat3(
    1, 0, 0,
    0, 1, 0,
    translation, 1
  );
  vec3 pos = transformMatrix * vec3(a_vertexPosition, 1);
  gl_Position = vec4(pos, 1);
}
```

因为Shader是在GPU中运算的，所以所有顶点都是被并行处理的。因此，通常情况下，在顶点着色器中执行缓动函数会更快。

#### 非线性插值

直接用JavaScript计算和放在顶点着色器里计算，差别也不是很大，但如果把它放在片元着色器里计算，因为要把每个像素点都计算一遍，所以性能消耗反而更大一些。

**那为什么还要在着色器中计算easing呢？**不仅可以利用easing控制动画过程，还可以在片元着色器中用easing来实现非线性的插值。

**什么是非线性插值呢？**

在正常情况下，顶点着色器定义的变量在片元着色器中，都会被线性插值。

**e.g.线性插值**

```glsl
// vertex
attribute vec2 a_vertexPosition;
attribute vec4 color;

varying vec4 vColor;

void main() {
  gl_PointSize = 1.0;
  vColor = color;
  gl_Position = vec4(a_vertexPosition, 1, 1);
}

// fragment
#ifdef GL_ES
precision highp float;
#endif

varying vec4 vColor;

void main() {
  gl_FragColor = vColor;
}
```

```javascript
bufferData({
  positions: [
    [-0.5, -0.25],
    [-0.5, 0.25],
    [0.5, 0.25],
    [0.5, -0.25]
  ],
  attributes: {
    color: [
        [1, 0, 0, 1],
        [1, 0, 0, 1],
        [0, 0.5, 0, 1],
        [0, 0.5, 0, 1]
    ],
  }
});
```

执行以上代码，会得到一个长方形，它的颜色会从左到右，由红色线性地过渡到绿色。

**e.g.非线性插值**

如果想要**实现非线性的颜色过渡**，可以采用uniform的方式，通过easing函数来实现。

```glsl
// vertex
attribute vec2 a_vertexPosition;
attribute vec2 uv;

varying vec2 vUv;

void main() {
  gl_PointSize = 1.0;
  vUv = uv;
  gl_Position = vec4(a_vertexPosition, 1, 1);
}
// fragment
#ifdef GL_ES
precision highp float;
#endif

float easing(in float p) {
  return smoothstep(0.0, 1.0, p);
  // return clamp(p * p, 0.0, 1.0);
  // return clamp(p * (2.0 - p), 0.0, 1.0);
}

varying vec2 vUv;
uniform vec4 fromColor;
uniform vec4 toColor;

void main() {
  float d = easing(vUv.x);
  gl_FragColor = mix(fromColor, toColor, d);
}
```

可以使用smoothstep等easing函数实现非线性插值。

**e.g.贝塞尔曲线缓动**

可以像利用JavaScript那样，在Shader里实现贝塞尔曲线缓动。

```glsl
// fragment
#ifdef GL_ES
precision highp float;
#endif

float easing(in float p) {
  return smoothstep(0.0, 1.0, p);
  // return clamp(p * p, 0.0, 1.0);
  // return clamp(p * (2.0 - p), 0.0, 1.0);
}
${bezier.cubic_bezier}
${color.hsb}

varying vec2 vUv;

void main() {
  // float d = easing(vUv.x);
  // float d = vUv.x;
  float d = cubic_bezier(vUv.x, 0.5, -1.5, 0.5, 2.5);
  gl_FragColor.rgb = hsb2rgb(vec3(d, 1.0, 1.0));
  gl_FragColor.a = 1.0;
}
```

效果：贝塞尔曲线插值色带

使用贝塞尔曲线缓动函数，能够实现更加丰富多彩的插值效果。



### 如何在片元着色器中实现随机粒子动画

使用片元着色器还可以实现非常复杂的图形动画，包括粒子动画、网格动画以及网格噪声动画等等。

**e.g.使用随机 + 噪声来实现一个粒子效果。**

```glsl
// fragment
#ifdef GL_ES
precision highp float;
#endif

varying vec2 vUv;
uniform float uTime;

highp float random(vec2 co) {
  highp float a = 12.9898;
  highp float b = 78.233;
  highp float c = 43758.5453;
  highp float dt = dot(co.xy, vec2(a, b));
  highp float sn = mod(dt, 3.14);
  return fract(sin(sn) * c);
}

${noise.noise_value}

float sdf_circle(vec2 st, vec2 c, float r) {
  return 1.0 - length(st - c) / r;
}

void main() {
  vec2 st = vUv;
  float rx = mix(
    -0.2,
    0.2,
    noise(vec2(7881.32, 0) + random(st) + uTime)
  );
  float ry = mix(
    -0.2,
    0.2,
    noise(vec2(0, 1433.59) + random(st) + uTime)
  );
  float dis = distance(st, vec2(0.5)); // 和点(0.5,0.5)的距离
  dis = pow((1.0 - dis), 2.0);
  float d = sdf_circle(
    st + vec2(rx, ry), // 使用噪声形成位移
    vec2(0.5),         // 圆心
    0.2                // 半径
  );
  d = smoothstep(0.0, 1.0, d);
  gl_FragColor = vec4(dis * d * vec3(1.0), 1.0);
}
```

首先，设置随机数 用来生成距离场的初始值；

然后，设置噪声用来形成位移；

最后，传入uTime变量来实现动画。

**效果：**能绘制出数量非常多的点，并且让它们沿着随机轨迹运动。

实现的过程中会涉及非常多点的运算，如果不用Shader，几乎是无法完成的（流畅的动画效果）。



### 要点总结

实现固定帧动画：可以把准备好的图片作为纹理，然后动态修改纹理坐标。

实现非固定帧动画：可以通过uniform，将变化的属性作为参数传给着色器处理。

与用HTML/CSS、SVG、Canvas2D实现动画的基本原理是一样的。只不过，WebGL中的很多计算，是需要用JavaScript和GLSL，也就是Shader来配合进行的。



### 小试牛刀

1. 完善矩阵运算，来实现缩放、旋转、平移和扭曲变换，以及它们的组合效果
2. 试着实现一个粒子效果：让一张图片从中心爆炸开来，炸成碎片并最终消失。