## 14 | 如何使用片元着色器进行几何造型？

片元着色器：

它是并行计算的，所以它和常规代码顺序执行或者串行执行过程并不一样。



### 如何用片元着色器<span style="color:orange;">控制局部颜色</span>？

最简单的就是把图片绘制为纯色。

```glsl
// 片元着色器
// ...
void main() {
  gl_FragColor = vec4(0, 0, 0, 1);
}
```

让一张图片呈现不同的颜色：根据纹理坐标值来绘制。

**e.g.由黑向白过渡**

```glsl
// ...
void main() {
  gl_FragColor.rgb = vec3(vUv.x);
  gl_FragColor.a = 1.0;
}
```

可以改变一下渲染方式让图形呈现的效果更复杂。

**e.g.创造10x10的方格，每个格子左上角是绿色，右下角是红色，中间过渡色**

```glsl
// ...
void main() {
  vec2 st = vUv * 10.0;
  gl_FragColor.rgb = vec3(fract(st), 0.0); // r和g取坐标值，b为0
  gl_FragColor.a = 1.0;
}
```

**e.g.通过`idx = floor(st)`获取网格的索引，判断网格索引除以2的余数（奇偶性），根据奇偶性来决定是否翻转网格内的x、y坐标。**

```glsl
// ...
void main() {
  vec2 st = vUv * 10.0;
  vec2 idx = floor(st); // 横纵坐标的整数部分（网格的索引）
  vec2 grid = fract(st); // 横纵坐标的小数部分

  // 在GLSL中，mod 函数是一个取模运算符，用于计算两个数值的除法运算的余数。
  vec2 t = mod(idx, 2.0);

  // 纹理坐标，左下角(0,0)，X轴向上 Y轴向右
  if (t.x == 1.0) { // 如果X轴方向索引是奇数，坐标反转
    grid.x = 1.0 - grid.x;
  }
  if (t.y == 1.0) { // 如果Y轴方向索引是奇数，坐标反转
    grid.y = 1.0 - grid.y;
  }
  gl_FragColor.rgb = vec3(grid, 0.0);
  gl_FragColor.a = 1.0;
}
```



一套统一的方法论。=> 在着色器里精确地绘制出我们想要的几何图形。

### 如何用片元着色器<span style="color:orange;">绘制圆、线段和几何图形</span>

几个例子。

#### 1. 绘制圆

根据**点坐标到圆心的距离**来生成颜色。

**e.g.一个模糊的圆**

```glsl
void main() {
  // distance函数：vUv和纹理坐标中心vec2(0.5)的距离
  float d = distance(vUv, vec2(0.5));
  // d越小，色值越接近黑色
  gl_FragColor.rgb = d * vec3(1.0);
  gl_FragColor.a = 1.0;
}
```

离圆心越近的点越黑。`d * vec3(1.0)`越接近0，色值越黑。

**e.g.用step函数基于0.2做阶梯，得到一个半径为0.2的圆。**（清晰）

```glsl
void main() {
  float d = distance(vUv, vec2(0.5));
   // 距离大于0.2时，step结果为0（黑色）；否则为1（白色）
  gl_FragColor.rgb = step(d, 0.2) * vec3(1.0);
  gl_FragColor.a = 1.0;
}
```

这个圆的边缘很不光滑。这是因为浮点数计算的精度导致的锯齿现象。

**e.g.用smoothstep代替step。**

与step的值是直接跳跃的不同，smoothstep在`step-start`和`step-end`之间有一个平滑过渡的区间。

```glsl
void main() {
  float d = distance(vUv, vec2(0.5));

  // float smoothstep(float edge0, float edge1, float x);
  // 参数说明：
  // - edge0：定义平滑区域的开始边缘。
  // - edge1：定义平滑区域的结束边缘。
  // - x：要测试的值。
  // 当`x`在`edge0`和`edge1`之间时，函数返回一个在0和1之间的平滑插值。
  // 如果`x`小于`edge0`，返回值是0；如果`x`大于`edge1`，返回值是1。
  // 这个函数通常用于在两个值之间创建一个平滑的过渡，例如在渲染中的纹理混合、颜色渐变或者边缘检测中。

  // ??? https://zhuanlan.zhihu.com/p/170493708
  // 当t1小于t2时: x小于t1返回0，当x大于t2返回1
  // 当t1大于t2时: x大于t1返回0，当x小于t2返回1 ✅
  // d大于0.205 ==> 0 黑色 ； d小于0.2 ==> 1 白色
  gl_FragColor.rgb = smoothstep(0.2 + 0.005, 0.2, d) * vec3(1.0);
  gl_FragColor.a = 1.0;
}
```

片元着色器绘制的圆，在构建图像的粒子效果中比较常用。

**e.g.实现图片的渐显渐隐效果。**

```glsl
// ...
void main() {
  vec2 uv = vUv;
  uv.y *= uResolution.y / uResolution.x; // y坐标乘以一个系数
  vec2 st = uv * 100.0; // 横纵坐标值放大100倍
  float d = distance(fract(st), vec2(0.5)); // 与对应网格中心点的距离
  float p = uTime + random(floor(st)); // 随机值
  float shading = 0.5 + 0.5 * sin(p); // 随机值为0的情况下：'0.5 - 1 - 0.5 - 0 - 0.5' 循环
  d = smoothstep(d, d + 0.01, shading); // 0~1
  vec4 color = texture2D(tMap, vUv); // 按纹理坐标取色值

  // T clamp(T x, float a,float b)    min(max(x, a), b)
  // float b = -0.2;
  // gl_FragColor.rgb = color.rgb * clamp(0.5, 1.3, b);

  // 在1.3 和 d + 1.0 * shading 之间变化
  // gl_FragColor.rgb = color.rgb * clamp(0.5, 1.3, d + 1.0 * shading);
  gl_FragColor.rgb = color.rgb * min(1.3, d + shading);
  gl_FragColor.a = color.a;
}
```

```javascript
// ...
renderer.uniforms.uTime = 0.0;
requestAnimationFrame(function update(t) {
  renderer.uniforms.uTime = t / 2000;
  requestAnimationFrame(update);
});
renderer.uniforms.uResolution = [1000, 554]; // 宽高
```

#### 2. 绘制线

**点到直线（向量）的距离**。

**e.g.绘制一条直线**

```glsl
void main() {
  vec3 line = vec3(1, 1, 0);
  // 两个向量叉积的绝对值
  // 点到直线（向量）的距离
  float d = abs(
    cross(vec3(vUv, 0), normalize(line)).z
  );

  // ??? https://zhuanlan.zhihu.com/p/170493708
  // 当t1小于t2时: x小于t1返回0，当x大于t2返回1
  // gl_FragColor.rgb = (1.0 - smoothstep(0.0, 0.01, d)) * vec3(1.0);

  // 当t1大于t2时: x大于t1返回0，当x小于t2返回1
  gl_FragColor.rgb = smoothstep(0.01, 0.0, d) * vec3(1.0);


  gl_FragColor.a = 1.0;
}
```

用一个三维向量line来定义一条直线。

两个二维向量叉积的Z轴分量的大小，就是这两个向量组成的平行四边形的面积，当我们把line的向量归一化后，这个值就是vUv到直线的距离了。

#### 3. 用鼠标控制直线

**e.g.添加一个uniform变量uMouse，根据鼠标位置来控制直线方向。**

```glsl
void main() {
  vec3 line = vec3(uMouse, 0); // 用向量表示所在直线
  float d = abs(
    cross(vec3(vUv, 0), normalize(line)).z
  ); // 叉积表示平行四边形面积
  
  gl_FragColor.rgb = smoothstep(0.01, 0.0, d) * vec3(1.0);
  gl_FragColor.a = 1.0;
}
```

在JavaScript中通过uniforms传入uMouse。

```javascript
// ...
renderer.uniforms.uMouse = [-1, -1]; // 鼠标移动点
// ...
glRef.value.addEventListener('mousemove', e => {
  const {width, height} = e.target.getBoundingClientRect();
  const {offsetX: x, offsetY: y} = e;
  renderer.uniforms.uMouse = [
    x / width,
    (height - y) / height
  ];
});
```

**e.g.让直线经过任意的定点。**

加一个uniform变量uOrigin，来表示直线经过的固定点。

```glsl
void main() {
  // ...

  // 原点变化
  vec3 line = vec3(uMouse - uOrigin, 0);
  float d = abs(
    cross(vec3(vUv - uOrigin, 0), normalize(line)).z
  );

  gl_FragColor.rgb = smoothstep(0.01, 0.0, d) * vec3(1.0);
  gl_FragColor.a = 1.0;
}
```

在JavaScript中通过uniforms传入uOrigin，并通过鼠标点击事件更新。

```javascript
// ...
renderer.uniforms.uOrigin = [0, 0]; // 设置原点
// ...
glRef.value.addEventListener('click', e => {
  const {width, height} = e.target.getBoundingClientRect();
  const {offsetX: x, offsetY: y} = e;
  renderer.uniforms.uOrigin = [
    x / width,
    (height - y) / height
  ];
});
```

**绘制线段：点到线段的距离。**

点和线段之间有两种关系，一种是点在线段上，另一种是在线段之外。

* 当点到线段的投影位于线段两个端点中间的时候，它就等于点到直线的距离；
* 当点到线段的投影在两个端点之外的时候，它就等于这个点到最近一个端点的距离。

**e.g.在原本片元着色器代码的基础上，抽象出一个seg_distance函数，用来返回点到线段的距离。**

```glsl
// 点到线段的距离
float seg_distance(in vec2 st, in vec2 a, in vec2 b) {
  vec3 ab = vec3(b - a, 0); // 目标线段所在向量
  vec3 p = vec3(st - a, 0); // 待检测向量
  float l = length(ab); // 向量长度
  float d = abs(
    cross(p, normalize(ab)).z
  ); // p到目标向量的距离
  float proj = dot(p, ab) / l; // p在目标向量上的投影
  if(proj >= 0.0 && proj <= l) return d; // 投影在目标线段两个端点之间
  // 在Shader中，distance函数用于计算两个点之间的欧几里得距离
  return min(distance(st, a), distance(st, b)); // p在线段左边或右边时（距离两个端点的距离）
}

void main() {
  float d = seg_distance(vUv, uOrigin, uMouse);
  gl_FragColor.rgb = smoothstep(0.01, 0.0, d) * vec3(1.0);
  gl_FragColor.a = 1.0;
}
```

#### 4. 绘制三角形

以上内容原理：求点到点或者点到线段距离

以下：封闭平面图形的绘制

**e.g.绘制三角形。**

* 首先，判断点是否在三角形内部。
* 然后，建立三角形的距离模型。
  * 规定内部距离为负，外部距离为正
  * 选点到三条边的最小距离

```glsl
// 点到直线的距离
float line_distance(in vec2 st, in vec2 a, in vec2 b) {
  // ...
}

// 点到线段的距离
float seg_distance(in vec2 st, in vec2 a, in vec2 b) {
  // ...
}

// 点与三角形的距离
float triangle_distance(in vec2 st, in vec2 a, in vec2 b, in vec2 c) {
  float d1 = line_distance(st, a, b);
  float d2 = line_distance(st, b, c);
  float d3 = line_distance(st, c, a);

  if (d1 >= 0.0 && d2 >= 0.0 && d3 >= 0.0
    || d1 <= 0.0 && d2 <= 0.0 && d3 <= 0.0) { // 三个叉积单位一致
    return -min(abs(d1), min(abs(d2), abs(d3))); // 内部距离为负
  }

  return min(seg_distance(st, a, b), min(seg_distance(st, b, c), seg_distance(st, c, a))); // 外部距离为正
}

void main() {
  float d = triangle_distance(
    vUv,
    vec2(0.3),
    vec2(0.5, 0.7),
    vec2(0.7, 0.3)
  );
  gl_FragColor.rgb = smoothstep(0.01, 0.0, d) * vec3(1.0);
  gl_FragColor.a = 1.0;
}
```

绘制出一个白色的三角形。

三角形的这种画法还可以推广到任意凸多边形。比如矩形和正多边形。



### 片元着色器绘图方法论：<span style="color:orange;">符号距离场渲染</span>

SDF：Signed Distance Field

前面绘制的图形虽然各不相同，但是它们的绘制步骤都可以总结为以下两步。

* 第一步：定义距离。

  这里的距离，是一个认为定义的概念。

* 第二步：根据距离着色。

  用smoothstep方法，选择某个范围的距离值。=> 将图形的边界绘制出来。

  延续这个思路，还可以选择距离在0.0~0.01范围以外的点。

  **e.g.两条对称的直线。**

  ```glsl
  void main() {
    vec3 line = vec3(1, 1, 0);
    float d = abs(
      cross(vec3(vUv, 0), normalize(line)).z
    );
    // smoothstep(0.195, 0.2, d)
    // 距离直线距离小于0.195的点 => 0           距离直线距离大于0.2的点 => 1
    // smoothstep(0.2, 0.205, d)
    // 距离直线距离小于0.2的点 => 0              距离直线距离大于0.205的点 => 1
  
    // (smoothstep(0.195, 0.2, d) - smoothstep(0.2, 0.205, d))
    // 距离小于0.195       => 0
    // 距离大于0.195小于0.2 => 0~1插值
    // 距离等于0.2         => 1
    // 距离大于0.2小于0.205 => 1 - 0~1插值
    // 距离大于0.205       => 0
    gl_FragColor.rgb = (smoothstep(0.195, 0.2, d) - smoothstep(0.2, 0.205, d)) * vec3(1.0);
    gl_FragColor.a = 1.0;
  }
  ```

  对距离原直线0.2处的点进行的着色。

  拓展：加上使用乘法和fract函数重复绘制的原理，可以绘制多条平行线。

  ```glsl
  void main() {
    vec3 line = vec3(1, 1, 0);
    float d = abs(
      cross(vec3(vUv, 0), normalize(line)).z
    );
    d = fract(20.0 * d); // 距离放大20倍，再取小数部分
  
    // smoothstep(0.45, 0.5, d) - smoothstep(0.5, 0.55, d)
    // 距离小于0.45       => 0
    // 距离大于0.45小于0.5 => 0~1插值
    // 距离等于0.5         => 1
    // 距离大于0.5小于0.55 => 1 - 0~1插值
    // 距离大于0.55       => 0
  
    // 0.5/20=0.025 0.025x3(0.075) 0.025x5(0.125) ...
    gl_FragColor.rgb = (smoothstep(0.45, 0.5, d) - smoothstep(0.5, 0.55, d)) * vec3(1.0);
    gl_FragColor.a = 1.0;
  }
  ```

  利用同样的办法，还可以绘制圆环或者三角环或者其他图形的环。

  **e.g.圆环**

  ```glsl
  void main() {
    float d = distance(vUv, vec2(0.5)); // 距离圆心的距离
    d = fract(20.0 * (d));
    gl_FragColor.rgb = (smoothstep(0.45, 0.5, d) - smoothstep(0.5, 0.55, d)) * vec3(1.0);
    // gl_FragColor.rgb = vec3(d);
    gl_FragColor.a = 1.0;
  }
  ```

  **e.g.三角环**

  ```glsl
  // ...
  
  void main() {
    float d = triangle_distance(vUv, vec2(0.3), vec2(0.5, 0.7), vec2(0.7, 0.3));
    // fract(float x) : x 减去整数部分，即可得到小数部分  x - floor(x) => 负数：1-x的小数部分
    // floor(-0.1) = -1  ==>  fract(-0.1) = -0.1 - (-1) = 0.9
  
    d = fract(20.0 * abs(d)); // if 20.0 * abs(d):0.5 => 原d:0.025 或 -0.025
    // 内部画两个三角形（垂心离某条边最近的距离 -> 数量：两个）
    // 本例三角形垂心：(0.5, 0.4)，内部距离至少小于 0.4-0.3=0.1 才能绘制出三角形 
  
    // d = fract(20.0 * d);   // if 20.0 * d: ±0.5     => 原d:0.025 或 -0.025
                              // if 20.0 * d: ±1.5     => 原d:0.075 或 -0.075
                              // if 20.0 * d: ±2.5     => 原d:0.125
                              // if 20.0 * d: ±3.5     => 原d:0.175
  
    gl_FragColor.rgb = (smoothstep(0.45, 0.5, d) - smoothstep(0.5, 0.55, d)) * vec3(1.0);
  
    gl_FragColor.a = 1.0;
  }
  ```

  > 垂心坐标公式：https://wenda.so.com/q/1638376615216229
  >
  > A(x1,y1)B(x2,y2)C(x3,y3)，垂心H(x0,y0)
  >
  > 用斜率是负倒数关系Kbc=y3-y2/x3-x2 Kah=y1-y0/x1-x0 Kah=-1/Kbc
  >
  > 得到方程(y3-y2)/(x3-x2)=-(x1-x0)/(y1-y0)
  >
  > 同理可得方程(y2-y1)/(x2-x1)=-(x3-x0)/(y3-y0)，解出x0，y0即可。

上面这种绘制图形和环的方式，在图形渲染中有一个专有的名称叫做**符号距离场渲染**（Signed Distance Fields Rendering）。本质上是**利用空间中的距离分布来着色**。

e.g.

```glsl
void main() {
  // ...
  
  // gl_FragColor.rgb = (smoothstep(0.45, 0.5, d) - smoothstep(0.5, 0.55, d)) * vec3(1.0);
  gl_FragColor.rgb = vec3(d);

  gl_FragColor.a = 1.0;
}
```

这里的每一环，两两之间的距离是沿着**法线方向**从0到1的，所以颜色从黑色过渡到白色，这就是三角环的距离场分布。

相同颜色值的环线就是距离场的等距线



### 着色器绘制几何图形的用途

这些片元着色器的绘图方式，究竟有什么实际用途呢？

着色器造型是着色器的一种非常基础的使用方法，甚至可以说是图形学中着色器渲染最基础的原理，就好比代数的基础是四则运算一样，它构成了GPU视觉渲染的基石。

要彻底弄明白它的原理和思路，尤其是非常重要的符号距离场渲染技巧

以下是三个简单的案例：

#### 1. 实现图像的裁剪

```glsl
// ...

void main() {
  vec4 color = texture2D(tMap, vUv);
  // 以平移后的点，计算当前点的色值
  vec2 uv = vUv - vec2(0.5);
  vec2 a = vec2(-0.577, 0) - vec2(0.5);
  vec2 b = vec2(0.5, 1.866) - vec2(0.5);
  vec2 c = vec2(1.577, 0) - vec2(0.5);

  float scale = min(1.0, 0.0005 * uTime);
  // 点 与 三角形的距离
  float d = triangle_distance(uv, scale * a, scale * b, scale * c);
  // 当t1小于t2时: x小于t1返回0，当x大于t2返回1
  gl_FragColor.rgb = (1.0 - smoothstep(0.0, 0.01, d)) * color.rgb;
  gl_FragColor.a = 1.0;
}
```

```javascript
// ...
renderer.uniforms.tMap = texture;
update(0);
// ...
const update = t => {
  renderer.uniforms.uTime = t;
  requestAnimationFrame(update);
}
```

#### 2. 实现对图像的动态修饰

```glsl
// ...

void main() {
  vec4 color = texture2D(tMap, vUv);
  // 以平移后的点，计算当前点的色值
  vec2 uv = vUv - vec2(0.5);
  vec2 a = vec2(0, 1); // 与Y轴同向
  float time = 0.0005 * uTime;

  // 正弦=对边/斜边
  // 余弦=邻边/斜边
  vec2 b = vec2(sin(time), cos(time)); // 从Y轴方向开始顺时针旋转
  float d = 0.0;

  float c0 = cross(vec3(b, 0.0), vec3(a, 0.0)).z; // b与a（Y轴方向）的叉积
  float c1 = cross(vec3(uv, 0.0), vec3(a, 0.0)).z; // uv与a（Y轴方向）的叉积
  float c2 = cross(vec3(uv, 0.0), vec3(b, 0.0)).z; // uv与b的叉积

  // c0 > 0.0：向量b在右半边圆
  // c1 > 0.0：向量uv在右半边
  // c2 < 0.0：uv在b的左侧
  if (c0 > 0.0 && c1 > 0.0 && c2 < 0.0) {
    d = 1.0;
  }

  // c0< 0.0：向量b在左半边圆
  // (c1 >= 0.0 || c2 <= 0.0)：向量uv在右半边 或 uv在b的左侧
  if (c0 < 0.0 && (c1 >= 0.0 || c2 <= 0.0)) {
    d = 1.0;
  }

  gl_FragColor.rgb = color.rgb;
  // mix(a,  b,  c)：线性插值函数。a和b是两个输入的颜色或值，c是一个介于0和1之间的浮点数，表示插值的权重
  // 当c接近0时，返回a；当c接近1时，mix函数返回b；当c在0和1之间时，返回a和b的插值结果。
  // d:0 -> 变绿    d:1 -> 原色
  gl_FragColor.r *= mix(0.3, 1.0, d);
  gl_FragColor.a = mix(0.9, 1.0, d);
}
```

#### 3. 在一些3D场景中修饰几何体

e.g.给一个球体套一个外壳

![](../imgs/shader-ball.gif)



### 要点总结

使用片元着色器进行几何造型的2种常用方法：

* 通过控制局部颜色来绘制图案

  根据像素坐标来控制颜色变化，然后利用重复绘制的技巧，形成有趣的图案花纹。

* 定义并计算像素坐标的距离，然后根据距离来填充颜色

  符号距离场渲染。着色器造型生成图案的基础方法。



### 小试牛刀

用 t 方法来绘制正方形、正六角星（？）、椭圆。